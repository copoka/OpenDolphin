<?xml version="1.0" encoding="Shift_JIS"?>
<!-- XSL Stylesheet of MML for Java XSLT Transformer -->
<!-- 	Author: Kenji ARITA and Junzo SATO -->
<!-- 	Copyright (c) 2001-2002, Kenji ARITA and Junzo SATO. All rights reserved. -->
<!-- 	This stylesheet has been created based on the original work for MML by Kenji ARITA. -->
<!-- 	The author (J.S) made some modifications. -->

<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:xhtml="http://www.w3.org/1999/xhtml" xmlns:mmlCm="http://www.medxml.net/MML/SharedComponent/Common/1.0" xmlns:mmlNm="http://www.medxml.net/MML/SharedComponent/Name/1.0" xmlns:mmlFc="http://www.medxml.net/MML/SharedComponent/Facility/1.0" xmlns:mmlDp="http://www.medxml.net/MML/SharedComponent/Department/1.0" xmlns:mmlAd="http://www.medxml.net/MML/SharedComponent/Address/1.0" xmlns:mmlPh="http://www.medxml.net/MML/SharedComponent/Phone/1.0" xmlns:mmlPsi="http://www.medxml.net/MML/SharedComponent/PersonalizedInfo/1.0" xmlns:mmlCi="http://www.medxml.net/MML/SharedComponent/CreatorInfo/1.0" xmlns:mmlPi="http://www.medxml.net/MML/ContentModule/PatientInfo/1.0" xmlns:mmlBc="http://www.medxml.net/MML/ContentModule/BaseClinic/1.0" xmlns:mmlFcl="http://www.medxml.net/MML/ContentModule/FirstClinic/1.0" xmlns:mmlHi="http://www.medxml.net/MML/SharedComponent/HealthInsurance/1.0" xmlns:mmlLs="http://www.medxml.net/MML/ContentModule/Lifestyle/1.0" xmlns:mmlPc="http://www.medxml.net/MML/ContentModule/ProgressCourse/1.0" xmlns:mmlRd="http://www.medxml.net/MML/SharedComponent/RegisteredDiagnosis/1.0" xmlns:mmlSg="http://www.medxml.net/MML/ContentModule/Surgery/1.0" xmlns:mmlSm="http://www.medxml.net/MML/ContentModule/Summary/1.0" xmlns:mmlLb="http://www.medxml.net/MML/ContentModule/test/1.0" xmlns:mmlRp="http://www.medxml.net/MML/ContentModule/report/1.0" xmlns:mmlRe="http://www.medxml.net/MML/ContentModule/Referral/1.0" xmlns:mmlSc="http://www.medxml.net/MML/SharedComponent/Security/1.0" xmlns:claim="http://www.medxml.net/claim/claimModule/2.1" xmlns:claimA="http://www.medxml.net/claim/claimAmountModule/2.1">

<!--<xsl:stylesheet xmlns:xsl="http://www.w3.org/TR/WD-xsl">-->

<xsl:output method="html" indent="yes"/>

<!--************************************************************************-->
<!--	MML to HTML Stylesheet for Dolphin Project v0.1				-->
<!--************************************************************************-->
<!--************************************************************************-->
<!--	全体のテンプレート								-->
<!--************************************************************************-->
<!--
<xsl:template match="/">
	<xsl:apply-templates select="Mml" />
</xsl:template>
-->
<!-- ################## root for HTML output ################# -->
<xsl:template match="/">
	<html>
		<head><title>This HTML was generated by Java XSL transformer.</title></head>
		<body>
			<xsl:apply-templates select="Mml" />
		</body>
	</html>
</xsl:template>
<!--************************************************************************-->
<!--	Mmlのテンプレート							-->
<!--************************************************************************-->
<xsl:template match="Mml">
<!--
	<div>＜MML Version = <xsl:value-of select="@version"/>＞</div>
-->
	<xsl:apply-templates select="MmlHeader"/>
	<xsl:apply-templates select="MmlBody"/>
</xsl:template>

<!--************************************************************************-->
<!--	MmlHeaderのテンプレート						-->
<!--************************************************************************-->
	<xsl:template match="MmlHeader">
	</xsl:template>

<!--************************************************************************-->
<!--	MmlBodyのテンプレート												-->
<!--************************************************************************-->
	<xsl:template match="MmlBody">
		<xsl:for-each select="MmlModuleItem">
			<xsl:apply-templates/>
		</xsl:for-each>
	</xsl:template>

<!--************************************************************************-->
<!--	docInfoのテンプレート												-->
<!--************************************************************************-->
	<xsl:template match="docInfo">
		<table border="0" width="100%" height="30" cellspacing="2" cellpadding="4">
			<td bgcolor="#77AA99" align="center" valign="middle">
				<font size="+1"><b>
					<xsl:choose>
						<xsl:when test="title[.='reportTest']">
							検査報告
						</xsl:when>
						<xsl:otherwise>
							<xsl:value-of select="title"/>
						</xsl:otherwise>
					</xsl:choose>
				</b></font>
			</td>
		</table>
		<table>
			<tr>
				<td><b>記録者</b></td>
				<td><xsl:apply-templates select="mmlCi:CreatorInfo/mmlPsi:PersonalizedInfo/mmlPsi:personName/mmlNm:Name"/></td>
				<td>　</td>
				<td><b>施　設</b></td>
				<td><xsl:apply-templates select="mmlCi:CreatorInfo/mmlPsi:PersonalizedInfo/mmlFc:Facility"/></td>
			</tr>
			<tr>
				<td><b>記録日</b></td>
				<td><xsl:value-of select="confirmDate"/></td>
				<td>　</td>
				<td><b>診療科</b></td>
				<td><xsl:apply-templates select="mmlCi:CreatorInfo/mmlPsi:PersonalizedInfo/mmlDp:Department"/></td>
			</tr>
		</table>
	</xsl:template>

<!--************************************************************************-->
<!--	contentのテンプレート												-->
<!--************************************************************************-->
	<xsl:template match="content">
		<hr size="2"/>
		<xsl:apply-templates/>
	</xsl:template>

<!--************************************************************************-->
<!--	住所表現形式のテンプレート											-->
<!--************************************************************************-->
	<xsl:template match="mmlAd:Address">
		<xsl:choose>
			<xsl:when test="mmlAd:full">
				<xsl:value-of select="mmlAd:full"/><br/>
			</xsl:when>
			<xsl:otherwise>
				<xsl:value-of select="mmlAd:prefecture"/>
				<xsl:value-of select="mmlAd:city"/>
				<xsl:value-of select="mmlAd:town"/>
				<xsl:value-of select="mmlAd:homeNumber"/>
				<br/>
			</xsl:otherwise>
		</xsl:choose>
		<xsl:if test="mmlAd:zip">
			郵便番号:<xsl:value-of select="mmlAd:zip"/><br/>
		</xsl:if>
		<xsl:if test="countryCode">
			国コード:<xsl:value-of select="mmlAd:countryCode"/><br/>
		</xsl:if>
	</xsl:template>

<!--************************************************************************-->
<!--	電話番号表現形式のテンプレート										-->
<!--************************************************************************-->
	<xsl:template match="mmlPh:Phone">
		<xsl:if test="*">
			<xsl:value-of select="mmlPh:area"/>-<xsl:value-of select="mmlPh:city"/>-<xsl:value-of select="mmlPh:number"/>（<xsl:value-of select="@mmlPh:telEquipType"/>）<br/>
		</xsl:if>
		<xsl:if test="mmlPh:country">
			国番号:<xsl:value-of select="mmlPh:country"/><br/>
		</xsl:if>
		<xsl:if test="mmlPh:memo">
			メモ:<xsl:value-of select="mmlPh:memo"/><br/>
		</xsl:if>
	</xsl:template>

<!--************************************************************************-->
<!--	Id形式のテンプレート												-->
<!--************************************************************************-->
	<xsl:template match="mmlCm:Id">
		<xsl:value-of select='.'/>
<!--（<xsl:value-of select="@mmlCm:type"/>）-->
	</xsl:template>

<!--************************************************************************-->
<!--	外部参照形式のテンプレート											-->
<!--************************************************************************-->
<!--
	<xsl:template match="mmlCm:extRef">
		<br/>
		<xsl:element name="a">
			<xsl:attribute name="href">./dms130.cgi?contentName=<xsl:value-of select="@mmlCm:href"/></xsl:attribute>
			<xsl:attribute name="target">_blank</xsl:attribute>
			添付ファイル（<xsl:value-of select="@mmlCm:title"/>）
		</xsl:element>
		<br/>
	</xsl:template>
-->
	<xsl:template match="mmlCm:extRef">
		<xsl:element name="p">
			[外部参照あり]
		</xsl:element><br/>
	</xsl:template>
<!--************************************************************************-->
<!--	人名表現形式のテンプレート											-->
<!--************************************************************************-->
	<xsl:template match="mmlNm:Name">
		<xsl:choose>
			<xsl:when test="mmlNm:fullname">
				<xsl:value-of select="mmlNm:fullname"/>
			</xsl:when>
			<xsl:otherwise>
				<xsl:value-of select="mmlNm:family"/>
		       	<xsl:for-each select="mmlNm:middle">
					<xsl:value-of select='.'/>
				</xsl:for-each>
				<xsl:value-of select="mmlNm:given"/>
			</xsl:otherwise>
		</xsl:choose>
	</xsl:template>

<!--************************************************************************-->
<!--	施設情報形式のテンプレート											-->
<!--************************************************************************-->
	<xsl:template match="mmlFc:Facility">
		<xsl:for-each select="mmlFc:name">
			<xsl:value-of select='.'/>
		</xsl:for-each>
		<xsl:if test="mmlCm:Id">
			［<xsl:apply-templates select="mmlCm:Id"/>］
		</xsl:if>
	</xsl:template>

<!--************************************************************************-->
<!--	診療科情報形式のテンプレート										-->
<!--************************************************************************-->
	<xsl:template match="mmlDp:Department">
		<xsl:for-each select="mmlDp:name">
			<xsl:value-of select='.'/>
		</xsl:for-each>
		<xsl:if test="mmlCm:Id">
			［<xsl:apply-templates select="mmlCm:Id"/>］
		</xsl:if>
	</xsl:template>

<!--************************************************************************-->
<!--	個人情報形式のテンプレート										-->
<!--************************************************************************-->
	<xsl:template match="mmlPsi:PersonalizedInfo">
		<xsl:if test="mmlPsi:personName">
			<xsl:for-each select="mmlPsi:personName/mmlNm:Name">
				<xsl:apply-templates select='.'/>
			</xsl:for-each>
		</xsl:if>
		<xsl:if test="mmlCm:Id">
			［<xsl:apply-templates select="mmlCm:Id"/>］
		</xsl:if>
		<br/>
		<xsl:if test="mmlFc:Facility">
			<xsl:apply-templates select="mmlFc:Facility"/><br/>
		</xsl:if>
		<xsl:if test="mmlDp:Department">
			<xsl:apply-templates select="mmlDp:Department"/><br/>
		</xsl:if>
		<xsl:if test="mmlPsi:address">
			<xsl:for-each select="mmlPsi:address/mmlAd:Address">
				住所：<xsl:value-of select='.'/><br/>
			</xsl:for-each>
		</xsl:if>
		<xsl:if test="mmlPsi:emailAddresses">
			<xsl:for-each select="mmlPsi:emailAddresses/mmlCm:email">
				EMAILアドレス：<xsl:value-of select='.'/><br/>
			</xsl:for-each>
		</xsl:if>
		<xsl:if test="mmlPsi:phones">
			<xsl:apply-templates select="mmlPh:Phone"/><br/>
		</xsl:if>
	</xsl:template>

<!--************************************************************************-->
<!--	作成者情報形式のテンプレート										-->
<!--************************************************************************-->
	<xsl:template match="mmlCi:CreatorInfo">
		<xsl:apply-templates select="mmlPsi:PersonalizedInfo"/>
		<xsl:for-each select="mmlCi:creatorLicense">
			<xsl:apply-templates/>
		</xsl:for-each>
	</xsl:template>

<!--************************************************************************-->
<!--	XHTML形式のテンプレート												-->
<!--************************************************************************-->
	<xsl:template match="xhtml:br">
		<xsl:element name="br"/>
<!--	XML::XSLTの変換がおかしい
		<xsl:element name="br">
			<xsl:if test="@xhtml:clear">
				<xsl:attribute name="clear"><xsl:value-of select="@xhtml:clear"/></xsl:attribute>
			</xsl:if>
		</xsl:element>
-->
	</xsl:template>

	<xsl:template match="xhtml:font">
		<xsl:element name="font">
			<xsl:if test="@xhtml:size">
				<xsl:attribute name="size"><xsl:value-of select="@xhtml:size"/></xsl:attribute>
			</xsl:if>
			<xsl:if test="@xhtml:color">
				<xsl:attribute name="color"><xsl:value-of select="@xhtml:color"/></xsl:attribute>
			</xsl:if>
			<xsl:if test="@xhtml:face">
				<xsl:attribute name="face"><xsl:value-of select="@xhtml:face"/></xsl:attribute>
			</xsl:if>
			<xsl:apply-templates/>
		</xsl:element>
	</xsl:template>

	<xsl:template match="xhtml:i">
		<xsl:element name="i">
			<xsl:apply-templates/>
		</xsl:element>
	</xsl:template>

	<xsl:template match="xhtml:b">
		<xsl:element name="b">
			<xsl:apply-templates/>
		</xsl:element>
	</xsl:template>

	<xsl:template match="xhtml:u">
		<xsl:element name="u">
			<xsl:apply-templates/>
		</xsl:element>
	</xsl:template>

	<xsl:template match="text()">
		<xsl:value-of select='.'/>
	</xsl:template>

<!--************************************************************************-->
<!--	PatientModuleのテンプレート											-->
<!--************************************************************************-->
	<xsl:template match="mmlPi:PatientModule">
		<table border="1" cellspacing="0" cellpadding="4">
			<!--	ＩＤ情報の処理			-->
			<xsl:apply-templates select="mmlPi:uniqueInfo"/>
			<!--	氏名情報の処理			-->
			<xsl:apply-templates select="mmlPi:personName"/>
			<!--	生年月日				-->
			<xsl:apply-templates select="mmlPi:birthday"/>
			<!--	性別の処理				-->
			<xsl:apply-templates select="mmlPi:sex"/>
			<!--	国籍の処理				-->
			<xsl:apply-templates select="mmlPi:nationality"/>
			<!--	婚姻状態の処理			-->
			<xsl:apply-templates select="mmlPi:marital"/>
			<!--	住所の処理				-->
			<xsl:apply-templates select="mmlPi:addresses"/>
			<!--	メールアドレスの処理	-->
			<xsl:apply-templates select="mmlPi:emailAddresses"/>
			<!--	電話番号の処理			-->
			<xsl:apply-templates select="mmlPi:phones"/>
			<!--	会計番号の処理			-->
			<xsl:apply-templates select="mmlPi:accountNumber"/>
			<!--	社会番号の処理			-->
			<xsl:apply-templates select="mmlPi:socialIdentification"/>
			<!--	死亡フラグの処理		-->
			<xsl:apply-templates select="mmlPi:death"/>
		</table>
	</xsl:template>

	<!--	ＩＤ情報のテンプレート			-->
	<xsl:template match="mmlPi:uniqueInfo">
		<tr>
			<th bgcolor="#aaaaaa" width="100">患者ID</th>
			<td><xsl:apply-templates select="mmlPi:masterId/mmlCm:Id"/></td>
		</tr>
		<xsl:if test="mmlPi:otherId">
			<tr>
				<th bgcolor="#aaaaaa">その他のID</th>
				<td>
					<xsl:for-each select="mmlPi:otherId">
						<table border="0" cellspacing="0" cellpadding="0"><tr>
							<td><xsl:apply-templates select="mmlCm:Id"/>（<xsl:value-of select="@mmlPi:type"/>）</td>
						</tr></table>
					</xsl:for-each>
				</td>
			</tr>
		</xsl:if>
	</xsl:template>

	<!--	氏名情報のテンプレート			-->
	<xsl:template match="mmlPi:personName">
		<tr>
			<th bgcolor="#aaaaaa">氏名</th>
			<td>
				<xsl:for-each select="mmlNm:Name">
					<table border="0" cellspacing="0" cellpadding="0"><tr>
						<td><xsl:apply-templates select='.'/></td>
					</tr></table>
				</xsl:for-each>
			</td>
		</tr>
	</xsl:template>

	<!--	生年月日のテンプレート			-->
	<xsl:template match="mmlPi:birthday">
   		<tr>
			<th bgcolor="#aaaaaa">生年月日</th>
			<td><xsl:value-of select='.'/></td>
		</tr>
	</xsl:template>

	<!--	性別のテンプレート				-->
	<xsl:template match="mmlPi:sex">
   		<tr>
			<th bgcolor="#aaaaaa">性別</th>
			<td>
				<xsl:choose>
					<xsl:when test = ".='female'">女性</xsl:when>
					<xsl:when test = ".='male'">男性</xsl:when>
					<xsl:when test = ".='other'">その他</xsl:when>
					<xsl:when test = ".='unknown'">不明</xsl:when>
					<xsl:otherwise>
						<xsl:value-of select='.'/>
					</xsl:otherwise>
				</xsl:choose>
			</td>
		</tr>
	</xsl:template>

	<!--	国籍のテンプレート				-->
	<xsl:template match="mmlPi:nationality">
   		<tr>
			<th bgcolor="#aaaaaa">国籍</th>
			<td><xsl:value-of select='.'/>/<xsl:value-of select="@mmlPi:subtype"/></td>
		</tr>
	</xsl:template>

	<!--	婚姻状態のテンプレート			-->
	<xsl:template match="mmlPi:marital">
   		<tr>
			<th bgcolor="#aaaaaa">婚姻</th>
			<td><xsl:value-of select='.'/></td>
		</tr>
	</xsl:template>

	<!--	住所のテンプレート				-->
	<xsl:template match="mmlPi:addresses">
		<tr>
			<th bgcolor="#aaaaaa">住所</th>
			<td>
				<xsl:for-each select="mmlAd:Address">
					<table border="0" cellspacing="0" cellpadding="0"><tr>
						<td><xsl:apply-templates select='.'/></td>
					</tr></table>
				</xsl:for-each>
			</td>
		</tr>
	</xsl:template>

	<!--	メールアドレスのテンプレート	-->
	<xsl:template match="mmlPi:emailAddresses">
   		<tr>
			<th bgcolor="#aaaaaa">E-Mailアドレス</th>
			<td>
				<xsl:for-each select="mmlCm:email">
					<table border="0" cellspacing="0" cellpadding="0"><tr>
						<td><xsl:value-of select='.'/></td>
					</tr></table>
				</xsl:for-each>
			</td>
		</tr>
	</xsl:template>

	<!--	電話番号のテンプレート		-->
	<xsl:template match="mmlPi:phones">
   		<tr>
			<th bgcolor="#aaaaaa">電話番号</th>
			<td>
				<xsl:for-each select="mmlPh:Phone">
					<table border="0" cellspacing="0" cellpadding="0"><tr>
						<td><xsl:apply-templates select='.'/></td>
					</tr></table>
				</xsl:for-each>
			</td>
		</tr>
	</xsl:template>

	<!--	会計番号のテンプレート		-->
	<xsl:template match="mmlPi:accountNumber">
   		<tr>
			<th bgcolor="#aaaaaa">会計番号</th>
			<td><xsl:value-of select='.'/></td>
		</tr>
	</xsl:template>

	<!--	社会番号のテンプレート		-->
	<xsl:template match="mmlPi:socialIdentification">
   		<tr>
			<th bgcolor="#aaaaaa">社会番号</th>
			<td><xsl:value-of select='.'/></td>
		</tr>
	</xsl:template>

	<!--	死亡フラグのテンプレート	-->
	<xsl:template match="mmlPi:death">
   		<tr>
			<th bgcolor="#aaaaaa">状態</th>
			<td>
				<xsl:choose>
					<xsl:when test=".='true'">
						<font color="ff0000">死亡</font>
					</xsl:when>
					<xsl:when test=".='false'">
						<font color="0000aa">生存</font>
					</xsl:when>
					<xsl:otherwise>
						<xsl:value-of select='.'/>
					</xsl:otherwise>
				</xsl:choose>
				<xsl:if test="@mmlPi:date">
					（<xsl:value-of select="@mmlPi:date"/>）
				</xsl:if>
			</td>
		</tr>
	</xsl:template>

<!--************************************************************************-->
<!--	HealthInsuranceModuleのテンプレート									-->
<!--************************************************************************-->
	<xsl:template match="mmlHi:HealthInsuranceModule">
		<table border="1" cellspacing="0" cellpadding="4">
			<!--	健康保険者番号の処理	-->
			<xsl:apply-templates select="mmlHi:insuranceNumber"/>
			<!--	被保険者情報の処理		-->
			<xsl:apply-templates select="mmlHi:clientId"/>
			<!--	本人家族区分の処理		-->
			<xsl:apply-templates select="mmlHi:familyClass"/>
			<!--	保険開始日の処理		-->
			<xsl:apply-templates select="mmlHi:startDate"/>
			<!--	有効期限の処理			-->
			<xsl:apply-templates select="mmlHi:expireDate"/>
			<!--	継続疾病情報の処理		-->
			<xsl:apply-templates select="mmlHi:continuedDiseases"/>
			<!--	負担率の処理			-->
			<xsl:apply-templates select="mmlHi:paymentRatio"/>
			<!--	公費負担医療情報の処理	-->
			<xsl:apply-templates select="mmlHi:publicInsurance"/>
		</table>
		<hr size="2"/>
	</xsl:template>

	<!--	健康保険者番号のテンプレート	-->
	<xsl:template match="mmlHi:insuranceNumber">
		<tr>
			<th bgcolor="#aaaaaa">保険者番号</th>
			<td><xsl:value-of select='.'/></td>
		</tr>
	</xsl:template>

	<!--	被保険者情報のテンプレート		-->
	<xsl:template match="mmlHi:clientId">
		<tr>
			<th bgcolor="#aaaaaa">被保険者情報</th>
			<td><xsl:value-of select='group'/> - <xsl:value-of select='number'/></td>
		</tr>
	</xsl:template>

	<!--	本人家族区分のテンプレート		-->
	<xsl:template match="mmlHi:familyClass">
   		<tr>
			<th bgcolor="#aaaaaa">本人家族区分</th>
			<td>
				<xsl:choose>
					<xsl:when test = ".='true'">本人</xsl:when>
					<xsl:when test = ".='false'">家族</xsl:when>
					<xsl:otherwise>
						<xsl:value-of select='.'/>
					</xsl:otherwise>
				</xsl:choose>
			</td>
		</tr>
	</xsl:template>

	<!--	保険開始日のテンプレート		-->
	<xsl:template match="mmlHi:startDate">
   		<tr>
			<th bgcolor="#aaaaaa">保険開始日</th>
			<td><xsl:value-of select='.'/></td>
		</tr>
	</xsl:template>

	<!--	有効期限のテンプレート			-->
	<xsl:template match="mmlHi:expireDate">
   		<tr>
			<th bgcolor="#aaaaaa">有効期限</th>
			<td><xsl:value-of select='.'/></td>
		</tr>
	</xsl:template>

	<!--	継続疾病情報のテンプレート		-->
	<xsl:template match="mmlHi:continuedDiseases">
   		<tr>
			<th bgcolor="#aaaaaa">継続疾病情報</th>
			<td>
				<table border="0" cellspacing="0" cellpadding="0">
					<xsl:for-each select="mmlHi:diseases">
						<tr><td><xsl:value-of select='.'/></td></tr>
					</xsl:for-each>
				</table>
			</td>
		</tr>
	</xsl:template>

	<!--	負担率のテンプレート			-->
	<xsl:template match="mmlHi:paymentRatio">
   		<tr>
			<th bgcolor="#aaaaaa">入院時負担率</th>
			<td><xsl:value-of select='.'/></td>
		</tr>
	</xsl:template>

	<!--	公費負担情報のテンプレート		-->
	<xsl:template match="mmlHi:publicInsurance">
   		<tr>
			<th bgcolor="#aaaaaa">公費負担情報</th>
			<td>
				<table border="0" cellspacing="0" cellpadding="0">
					<tr bgcolor="#aaaaaa"><th>負担者番号</th><th>受給者番号</th></tr>
					<xsl:for-each select="mmlHi:publicInsuranceItem">
						<tr>
							<td><xsl:value-of select='mmlHi:provider'/></td>
							<td><xsl:value-of select='mmlHi:recipient'/></td>
						</tr>
					</xsl:for-each>
				</table>
			</td>
		</tr>
	</xsl:template>

<!--************************************************************************-->
<!--	RegisteredDiagnosisModuleのテンプレート								-->
<!--************************************************************************-->
	<xsl:template match="mmlRd:RegisteredDiagnosisModule">
		<table border="1" cellspacing="0" cellpadding="3">
			<tr>
				<xsl:if test="mmlRd:diagnosis">
	           		<th bgcolor="#aaaaaa">疾患名</th>
					<td><xsl:apply-templates select="mmlRd:diagnosis"/></td>
				</xsl:if>
				<xsl:if test="mmlRd:diagnosisContents">
	           		<th bgcolor="#aaaaaa">疾患名</th>
					<td><xsl:apply-templates select="mmlRd:diagnosisContents"/></td>
				</xsl:if>
			</tr>
			<xsl:if test="mmlRd:categories">
				<tr>
					<th bgcolor="#aaaaaa">分類名</th>
					<td><xsl:apply-templates select="mmlRd:categories"/></td>
				</tr>
			</xsl:if>
			<xsl:if test="mmlRd:startDate">
				<tr>
					<th bgcolor="#aaaaaa">開始日</th>
					<td><xsl:value-of select="mmlRd:startDate"/></td>
				</tr>
			</xsl:if>
			<xsl:if test="mmlRd:endDate">
				<tr>
					<th bgcolor="#aaaaaa">終了日</th>
					<td><xsl:value-of select="mmlRd:endDate"/></td>
				</tr>
			</xsl:if>
			<xsl:if test="mmlRd:outcome">
				<tr>
					<th bgcolor="#aaaaaa">転帰</th>
					<td>
						<xsl:choose>
							<xsl:when test="mmlRd:outcome='died'">死亡</xsl:when>
							<xsl:when test="mmlRd:outcome='worsening'">悪化</xsl:when>
							<xsl:when test="mmlRd:outcome='unchanged'">不変</xsl:when>
							<xsl:when test="mmlRd:outcome='recovering'">回復</xsl:when>
							<xsl:when test="mmlRd:outcome='fullyRecovering'">全治</xsl:when>
							<xsl:when test="mmlRd:outcome='sequelae'">続発症</xsl:when>
							<xsl:when test="mmlRd:outcome='end'">終了</xsl:when>
							<xsl:when test="mmlRd:outcome='pause'">中止</xsl:when>
							<xsl:when test="mmlRd:outcome='continued'">継続</xsl:when>
							<xsl:when test="mmlRd:outcome='transfer'">転医</xsl:when>
							<xsl:when test="mmlRd:outcome='transferAcute'">急性病院へ転医</xsl:when>
							<xsl:when test="mmlRd:outcome='transferChronic'">慢性病院へ転医</xsl:when>
							<xsl:when test="mmlRd:outcome='home'">自宅等へ退院</xsl:when>
							<xsl:when test="mmlRd:outcome='unknown'">不明</xsl:when>
							<xsl:otherwise>
								<xsl:value-of select="mmlRd:outcome"/>
							</xsl:otherwise>
						</xsl:choose>
					</td>
				</tr>
			</xsl:if>
			<xsl:if test="mmlRd:firstEncounterDate">
				<tr>
					<th bgcolor="#aaaaaa">初診日</th>
					<td><xsl:value-of select="mmlRd:firstEncounterDate"/></td>
				</tr>
			</xsl:if>
		</table>
	</xsl:template>

	<!--	全体格納方式のテンプレート	-->
	<xsl:template match="mmlRd:diagnosis">
		<xsl:value-of select='.'/>［<xsl:value-of select="@mmlRd:code"/> : <xsl:value-of select="@mmlRd:system"/>］
	</xsl:template>

	<!--	要素別格納方式のテンプレート	-->
	<xsl:template match="mmlRd:diagnosisContents">
		<table border="0" cellspacing="0" cellpadding="0">
			<xsl:for-each select="mmlRd:dxItem">
				<tr><td><xsl:apply-templates select="mmlRd:name"/></td></tr>
			</xsl:for-each>
		</table>
	</xsl:template>

	<!--	疾病名要素のテンプレート		-->
	<xsl:template match="mmlRd:name">
		<xsl:value-of select='.'/>［<xsl:value-of select="@mmlRd:code"/> : <xsl:value-of select="@mmlRd:system"/>］
	</xsl:template>

	<!--	分類名のテンプレート	-->
	<xsl:template match="mmlRd:categories">
		<table border="0" cellspacing="0" cellpadding="0">
			<xsl:for-each select="mmlRd:category">
				<tr><td><xsl:value-of select='.'/></td></tr>
			</xsl:for-each>
		</table>
	</xsl:template>

<!--************************************************************************-->
<!--	LifestyleModuleのテンプレート										-->
<!--************************************************************************-->
	<xsl:template match="mmlLs:LifestyleModule">
		<!--	職業情報の処理	-->
		<xsl:apply-templates select="mmlLs:occupation"/>
		<!--	喫煙歴の処理	-->
		<xsl:apply-templates select="mmlLs:tobacco"/>
		<!--	飲酒歴の処理	-->
		<xsl:apply-templates select="mmlLs:alchol"/>
		<!--	その他の処理			-->
		<xsl:for-each select="mmlLs:other">
			<xsl:apply-templates/>
		</xsl:for-each>
	</xsl:template>

	<!--	職業情報のテンプレート		-->
	<xsl:template match="mmlLs:occupation">
		<div><font size="+1" color="#0000ff"><b>職業</b></font></div>
		<br/>
		<!-- XHTML使用可	-->
		<xsl:apply-templates/>
	</xsl:template>

	<!--	喫煙歴のテンプレート		-->
	<xsl:template match="mmlLs:tobacco">
		<div><font size="+1" color="#0000ff"><b>喫煙歴</b></font></div>
		<br/>
		<!-- XHTML使用可	-->
		<xsl:apply-templates/>
	</xsl:template>

	<!--	飲酒歴のテンプレート		-->
	<xsl:template match="mmlLs:alchol">
		<div><font size="+1" color="#0000ff"><b>飲酒歴</b></font></div>
		<br/>
		<!-- XHTML使用可	-->
		<xsl:apply-templates/>
	</xsl:template>

	<!--	その他情報のテンプレート	-->
	<xsl:template match="mmlLs:other">
		<div><font size="+1" color="#0000ff"><b>その他</b></font></div>
		<br/>
		<!-- XHTML使用可	-->
		<xsl:apply-templates/>
	</xsl:template>

<!--************************************************************************-->
<!--	BaseClinicModuleのテンプレート										-->
<!--************************************************************************-->
	<xsl:template match="mmlBc:LifestyleModule">
		<!--	アレルギー情報の処理	-->
		<xsl:apply-templates select="mmlBc:allergy"/>
		<!--	血液型情報の処理		-->
		<xsl:apply-templates select="mmlBc:bloodtype"/>
		<!--	感染症情報の処理		-->
		<xsl:apply-templates select="mmlBc:infection"/>
	</xsl:template>

	<!--	アレルギー情報のテンプレート	-->
	<xsl:template match="mmlBc:allergy">
		<div><font size="+1" color="#0000ff"><b>Allergy</b></font></div>
		<br/>
		<table border="1" cellspacing="0" cellpadding="3">
			<tr bgcolor="#aaaaaa">
           		<th>要因</th><th>反応</th><th>同定日</th><th>メモ</th>
			</tr>
			<xsl:for-each select="mmlBc:allergyItem">
				<tr>
					<td><xsl:value-of select="mmlBc:factor"/></td>
					<td><xsl:value-of select="mmlBc:serverity"/></td>
					<td><xsl:value-of select="mmlBc:identifiedDate"/></td>
					<td><xsl:apply-templates select="mmlBc:memo"/></td>
				</tr>
			</xsl:for-each>
		</table>
		<hr size="2"/>
	</xsl:template>

	<!--	血液型情報のテンプレート	-->
	<xsl:template match="mmlBc:bloodtype">
		<div><font size="+1" color="#0000ff"><b>BloodType</b></font></div>
		<br/>
		<table border="1" cellspacing="0" cellpadding="3">
			<tr>
           		<th bgcolor="#aaaaaa">ABO式血液型</th>
				<td><xsl:value-of select="mmlBc:abo"/></td>
			</tr>
			<xsl:if test="mmlBc:rh">
				<tr>
					<th bgcolor="#aaaaaa">Rh式血液型</th>
					<td><xsl:value-of select="mmlBc:rh"/></td>
				</tr>
			</xsl:if>
			<xsl:if test="mmlBc:others">
				<tr>
					<th bgcolor="#aaaaaa">その他血液型</th>
					<td><xsl:apply-templates select="mmlBc:others"/></td>
				</tr>
			</xsl:if>
			<xsl:if test="mmlBc:memo">
				<tr>
					<th bgcolor="#aaaaaa">メモ</th>
					<td><xsl:apply-templates select="mmlBc:memo"/></td>
				</tr>
			</xsl:if>
		</table>
		<hr size="2"/>
	</xsl:template>

	<!--	感染症情報のテンプレート	-->
	<xsl:template match="mmlBc:infection">
		<div><font size="+1" color="#0000ff"><b>Infection</b></font></div>
		<br/>
		<table border="1" cellspacing="0" cellpadding="3">
			<tr bgcolor="#aaaaaa">
           		<th>要因</th><th>検査値</th><th>同定日</th><th>メモ</th>
			</tr>
			<xsl:for-each select="mmlBc:infectionItem">
				<tr>
					<td><xsl:value-of select="mmlBc:factor"/></td>
					<td><xsl:value-of select="mmlBc:examValue"/></td>
					<td><xsl:value-of select="mmlBc:identifiedDate"/></td>
					<td><xsl:apply-templates select="mmlBc:memo"/></td>
				</tr>
			</xsl:for-each>
		</table>
		<hr size="2"/>
	</xsl:template>

	<!--	メモのテンプレート		-->
	<xsl:template match="mmlBc:memo">
		<!-- XHTML使用可	-->
		<xsl:apply-templates/>
	</xsl:template>

	<!--	その他血液型のテンプレート	-->
	<xsl:template match="mmlBc:others">
		<table border="1" cellspacing="0" cellpadding="3">
			<tr bgcolor="#aaaaaa">
	           		<th>名称</th><th>判定</th><th>注釈</th>
			</tr>
			<tr>
				<xsl:for-each select="mmlBc:other">
					<td><xsl:value-of select="mmlBc:typeName"/></td>
					<td><xsl:value-of select="mmlBc:typeJudgement"/></td>
					<!-- XHTML使用可	-->
					<td><xsl:apply-templates select="mmlBc:description"/></td>
				</xsl:for-each>
			</tr>
		</table>
	</xsl:template>

<!--************************************************************************-->
<!--	FirstClinicModuleのテンプレート										-->
<!--************************************************************************-->
	<xsl:template match="mmlFcl:FirstClinicModule">
		<!--	家族歴情報の処理	-->
		<xsl:apply-templates select="mmlFcl:familyHistory"/>
		<!--	小児期情報の処理	-->
		<xsl:apply-templates select="mmlFcl:childhood"/>
		<!--	既往歴情報の処理	-->
		<xsl:apply-templates select="mmlFcl:pastHistory"/>
		<!--	主訴の処理			-->
		<xsl:apply-templates select="mmlFcl:chiefComplaints"/>
		<!--	現病歴の処理		-->
		<xsl:apply-templates select="mmlFcl:presentIllnessNotes"/>
	</xsl:template>

	<!--	家族歴情報のテンプレート	-->
	<xsl:template match="mmlFcl:familyHistory">
		<div><font size="+1" color="#0000ff"><b>FamilyHistory</b></font></div>
		<br/>
		<table border="1" cellspacing="0" cellpadding="3">
			<tr bgcolor="#aaaaaa">
           		<th>続柄</th><th>疾患情報</th><th>年齢</th><th>メモ</th>
			</tr>
			<xsl:for-each select="mmlFcl:familyHistoryItem">
				<tr>
					<td><xsl:apply-templates select="mmlFcl:relation"/></td>
					<td><xsl:apply-templates select="mmlRd:RegisteredDiagnosisModule"/></td>
					<td><xsl:value-of select="mmlFcl:age"/></td>
					<td><xsl:apply-templates select="mmlFcl:memo"/></td>
				</tr>
			</xsl:for-each>
		</table>
		<hr size="2"/>
	</xsl:template>

	<!--	続柄のテンプレート	-->
	<xsl:template match="mmlFcl:relation">
		<xsl:choose>
			<xsl:when test=".='self'">本人</xsl:when>
			<xsl:when test=".='wife'">妻</xsl:when>
			<xsl:when test=".='husband'">夫</xsl:when>
			<xsl:when test=".='brother'">兄弟</xsl:when>
			<xsl:when test=".='sister'">姉妹</xsl:when>
			<xsl:when test=".='cousin'">いとこ</xsl:when>
			<xsl:when test=".='secondCousin'">はとこ</xsl:when>
			<xsl:when test=".='mother'">母</xsl:when>
			<xsl:when test=".='father'">父</xsl:when>
			<xsl:when test=".='aunt'">おば</xsl:when>
			<xsl:when test=".='uncle'">おじ</xsl:when>
			<xsl:when test=".='grandMother'">祖母</xsl:when>
			<xsl:when test=".='grandFather'">祖父</xsl:when>
			<xsl:when test=".='greatGrandMother'">曾祖母</xsl:when>
			<xsl:when test=".='greatGrandFather'">曾祖父</xsl:when>
			<xsl:when test=".='daughter'">娘</xsl:when>
			<xsl:when test=".='son'">息子</xsl:when>
			<xsl:when test=".='grandDaughter'">孫娘</xsl:when>
			<xsl:when test=".='grandSon'">孫息子</xsl:when>
			<xsl:when test=".='greatGrandDaughter'">曾孫娘</xsl:when>
			<xsl:when test=".='greatGrandSon'">曾孫息子</xsl:when>
			<xsl:when test=".='other'">その他</xsl:when>
			<xsl:otherwise>
				<xsl:value-of select="."/>
			</xsl:otherwise>
		</xsl:choose>
	</xsl:template>

	<!--	小児期情報のテンプレート	-->
	<xsl:template match="mmlFcl:childhood">
		<div><font size="+1" color="#0000ff"><b>Childhood</b></font></div>
		<br/>
		<xsl:apply-templates select="mmlFcl:birthInfo"/>
		<br/>
		<xsl:apply-templates select="mmlFcl:vaccination"/>
		<hr size="2"/>
	</xsl:template>

	<!--	出生時情報のテンプレート	-->
	<xsl:template match="mmlFcl:birthInfo">
		<b><u>出生時情報</u></b><br/>
		<xsl:if test="mmlFc:Facility">
			出生施設 : <xsl:apply-templates select="mmlFc:Facility"/>
		</xsl:if>
		<table border="1" cellspacing="0" cellpadding="3">
			<tr bgcolor="#aaaaaa">
				<th>週数</th><th>分娩方法</th><th>体重</th><th>身長</th><th>胸囲</th><th>頭囲</th><th>メモ</th>
			</tr>
			<tr>
				<td><xsl:value-of select="mmlFcl:deliveryWeeks"/></td>
				<td><xsl:value-of select="mmlFcl:deliveryMethod"/></td>
				<td><xsl:value-of select="mmlFcl:bodyWeigth"/> <xsl:value-of select="mmlFcl:bodyWeigth/@mmlFcl:unit"/></td>
				<td><xsl:value-of select="mmlFcl:bodyHeight"/> <xsl:value-of select="mmlFcl:bodyHeight/@mmlFcl:unit"/></td>
				<td><xsl:value-of select="mmlFcl:chestCircumference"/> <xsl:value-of select="mmlFcl:chestCircumference/@mmlFcl:unit"/></td>
				<td><xsl:value-of select="mmlFcl:headCircumference"/> <xsl:value-of select="mmlFcl:headCircumference/@mmlFcl:unit"/></td>
				<td><xsl:apply-templates select="mmlFcl:memo"/></td>
			</tr>
		</table>
	</xsl:template>

	<!--	予防接種情報のテンプレート	-->
	<xsl:template match="mmlFcl:vaccination">
		<b><u>予防接種情報</u></b><br/>
		<table border="1" cellspacing="0" cellpadding="3">
			<tr bgcolor="#aaaaaa">
                    <th>ワクチン</th><th>実施状態</th><th>年齢</th><th>メモ</th>
			</tr>
               	<xsl:for-each select="mmlFcl:vaccinationItem">
				<tr>
					<td><xsl:value-of select="mmlFcl:vaccine"/></td>
					<td>
						<xsl:choose>
							<xsl:when test="mmlFcl:injected='true'">
								<font color="0000aa">接種済み</font>
							</xsl:when>
							<xsl:when test="mmlFcl:injected='false'">
								<font color="ff0000">未接種</font>
							</xsl:when>
							<xsl:otherwise>
								<xsl:value-of select="mmlFcl:injected"/>
							</xsl:otherwise>
						</xsl:choose>
					</td>
					<td><xsl:value-of select="mmlFcl:age"/></td>
					<td><xsl:apply-templates select="mmlFcl:memo"/></td>
				</tr>
			</xsl:for-each>
		</table>
	</xsl:template>

	<!--	既往歴情報のテンプレート	-->
	<xsl:template match="mmlFcl:pastHistory">
		<div><font size="+1" color="#0000ff"><b>PastHistory</b></font></div>
		<br/>
		<xsl:if test="mmlFcl:freeNotes">
			<xsl:apply-templates select="mmlFcl:freeNotes"/>
		</xsl:if>
		<xsl:if test="mmlFcl:pastHistoryItem">
			<table border="1" cellspacing="0" cellpadding="3">
				<tr bgcolor="#aaaaaa">
					<th>時間</th><th>イベント</th>
				</tr>
	           	<xsl:for-each select="mmlFcl:pastHistoryItem">
					<tr>
						<td><xsl:value-of select="mmlFcl:timeExpression"/></td>
						<td>
							<table border="0" cellspacing="0" cellpadding="0">
								<xsl:for-each select="mmlFcl:eventExpression">
									<tr><td>
									<!-- XHTML使用可	-->
									<xsl:apply-templates/>
									</td></tr>
								</xsl:for-each>
							</table>
						</td>
					</tr>
				</xsl:for-each>
			</table>
		</xsl:if>
		<hr size="2"/>
	</xsl:template>

	<!--	主訴のテンプレート			-->
	<xsl:template match="mmlFcl:chiefComplaints">
		<div><font size="+1" color="#0000ff"><b>ChiefComplaints</b></font></div>
		<br/>
		<!-- XHTML使用可	-->
		<xsl:apply-templates/>
		<hr size="2"/>
	</xsl:template>

	<!--	現病歴のテンプレート		-->
	<xsl:template match="mmlFcl:presentIllnessNotes">
		<div><font size="+1" color="#0000ff"><b>PresentIllnessNotes</b></font></div>
		<br/>
		<!-- XHTML使用可	-->
		<xsl:apply-templates/>
		<hr size="2"/>
	</xsl:template>

	<!--	メモのテンプレート	-->
	<xsl:template match="mmlFcl:memo">
		<!-- XHTML使用可	-->
		<xsl:apply-templates/>
	</xsl:template>

	<!--	フリーノートのテンプレート	-->
	<xsl:template match="mmlFcl:freeNotes">
		<!-- XHTML使用可	-->
		<xsl:apply-templates/>
	</xsl:template>

<!--************************************************************************-->
<!--	ProgressCourseModuleのテンプレート									-->
<!--************************************************************************-->
	<xsl:template match="mmlPc:ProgressCourseModule">
		<!--	自由記載形式の処理		-->
		<xsl:apply-templates select="mmlPc:FreeExpression"/>
		<!--	構造化記載形式の処理	-->
		<xsl:apply-templates select="mmlPc:structuredExpression"/>
	</xsl:template>

	<!--	自由記載形式のテンプレート		-->
	<xsl:template match="mmlPc:FreeExpression">
		<div><font size="+1" color="#0000ff"><b>FreeExpression</b></font></div>
		<br/>
		<!-- XHTML使用可	-->
		<xsl:apply-templates/>
	</xsl:template>

	<!--	構造化記載形式のテンプレート	-->
	<xsl:template match="mmlPc:structuredExpression">
		<xsl:for-each select="mmlPc:problemItem">
			<xsl:apply-templates select="mmlPc:problem"/>
			<xsl:apply-templates select="mmlPc:subjective"/>
			<xsl:apply-templates select="mmlPc:objective"/>
			<xsl:apply-templates select="mmlPc:assessment"/>
			<xsl:apply-templates select="mmlPc:plan"/>
		</xsl:for-each>
	</xsl:template>

	<!--	プロブレムのテンプレート		-->
	<xsl:template match="mmlPc:problem">
		<div><font size="+1" color="#0000ff"><b>Problem</b></font></div>
		<br/>
		<!-- XHTML使用可	-->
		<xsl:apply-templates/>
		<hr size="2"/>
	</xsl:template>

	<!--	サブジェクティブのテンプレート	-->
	<xsl:template match="mmlPc:subjective">
		<div><font size="+1" color="#0000ff"><b>Subjective</b></font></div>
		<br/>
		<xsl:apply-templates select="mmlPc:freeNotes"/>
		<xsl:for-each select="mmlPc:subjectiveItem">
			<xsl:value-of select="mmlPc:timeExpression"/><br/>
			<xsl:for-each select="mmlPc:eventExpression">
				<!-- XHTML使用可	-->
				<xsl:apply-templates/>
			</xsl:for-each>
			<br/>
		</xsl:for-each>
		<hr size="2"/>
	</xsl:template>

	<!--	フリーノートのテンプレート	-->
	<xsl:template match="mmlPc:freeNotes">
		<!-- XHTML使用可	-->
		<xsl:apply-templates/>
	</xsl:template>

	<!--	オブジェクティブのテンプレート	-->
	<xsl:template match="mmlPc:objective">
		<div><font size="+1" color="#0000ff"><b>Objective</b></font></div>
		<br/>
		<xsl:apply-templates select="mmlPc:objectiveNotes"/>
		<xsl:apply-templates select="mmlPc:physicalExam"/>
		<xsl:for-each select="mmlPc:testResult">
			<!-- XHTML使用可	-->
			<xsl:apply-templates/>
			<xsl:for-each select="mmlCm:extRef">
				<xsl:apply-templates/>
			</xsl:for-each>
		</xsl:for-each>
		<xsl:for-each select="mmlPc:rxRecord">
			<!-- XHTML使用可	-->
			<xsl:apply-templates/>
			<xsl:for-each select="mmlCm:extRef">
				<xsl:apply-templates/>
			</xsl:for-each>
		</xsl:for-each>
		<xsl:for-each select="mmlPc:txRecord">
			<!-- XHTML使用可	-->
			<xsl:apply-templates/>
			<xsl:for-each select="mmlCm:extRef">
				<xsl:apply-templates/>
			</xsl:for-each>
		</xsl:for-each>
		<hr size="2"/>
	</xsl:template>

	<!--	オブジェクティブノートのテンプレート	-->
	<xsl:template match="mmlPc:objectiveNotes">
		<!-- XHTML使用可	-->
		<xsl:apply-templates/>
		<br/>
	</xsl:template>

	<!--	身体所見のテンプレート		-->
	<xsl:template match="mmlPc:physicalExam">
		<xsl:for-each select="mmlPc:physicalExamItem">
			<xsl:apply-templates/>
		</xsl:for-each>
		<br/>
	</xsl:template>

	<!--	身体所見アイテムのテンプレート	-->
	<xsl:template match="mmlPc:physicalExamItem">
		<xsl:value-of select="mmlPc:title"/><br/>
		<xsl:value-of select="mmlPc:result"/><br/>
		<xsl:for-each select="mmlPc:interpretation">
			<!-- XHTML使用可	-->
			<xsl:apply-templates/>
		</xsl:for-each>
		<br/>
		<xsl:value-of select="mmlPc:referrenceInfo"/>
		<br/>
	</xsl:template>

	<!--	アセスメントのテンプレート	-->
	<xsl:template match="mmlPc:assessment">
		<div><font size="+1" color="#0000ff"><b>Assesment</b></font></div>
		<br/>
		<xsl:for-each select="mmlPc:assessmentItem">
			<!-- XHTML使用可	-->
			<xsl:apply-templates/>
		</xsl:for-each>
		<hr size="2"/>
	</xsl:template>

	<!--	プランのテンプレート		-->
	<xsl:template match="mmlPc:plan">
		<div><font size="+1" color="#0000ff"><b>Plan</b></font></div>
		<br/>
		<xsl:for-each select="mmlPc:testOrder">
			<!-- XHTML使用可	-->
			<xsl:apply-templates/>
			<xsl:for-each select="mmlCm:extRef">
				<xsl:apply-templates/>
			</xsl:for-each>
		</xsl:for-each>
		<xsl:for-each select="mmlPc:rxOrder">
			<!-- XHTML使用可	-->
			<xsl:apply-templates/>
			<xsl:for-each select="mmlCm:extRef">
				<xsl:apply-templates/>
			</xsl:for-each>
		</xsl:for-each>
		<xsl:for-each select="mmlPc:txOrder">
			<!-- XHTML使用可	-->
			<xsl:apply-templates/>
			<xsl:for-each select="mmlCm:extRef">
				<xsl:apply-templates/>
			</xsl:for-each>
		</xsl:for-each>
		<xsl:apply-templates select="mmlPc:planNotes"/>
		<hr size="2"/>
	</xsl:template>

	<!--	プランノートのテンプレート	-->
	<xsl:template match="mmlPc:planNotes">
		<!-- XHTML使用可	-->
		<xsl:apply-templates/>
		<br/>
	</xsl:template>

<!--************************************************************************-->
<!--	ReferralModuleのテンプレート										-->
<!--************************************************************************-->
	<xsl:template match="mmlRe:ReferralModule">
		<!--	紹介先情報の処理		-->
		<div><font size="+1" color="#0000ff"><b>紹介先情報</b></font></div>
		<br/>
		<xsl:apply-templates select="mmlRe:referToFacility"/>
		<xsl:apply-templates select="mmlRe:referToPerson"/>
		<xsl:apply-templates select="mmlRe:referToUnknownName"/>
		<hr size="2"/>
		<!--	紹介元情報の処理			-->
		<div><font size="+1" color="#0000ff"><b>紹介元情報</b></font></div>
		<br/>
		<xsl:apply-templates select="mmlRe:referFrom"/>
		<hr size="2"/>
		<!--	患者情報モジュールの処理	-->
		<div><font size="+1" color="#0000ff"><b>患者情報</b></font></div>
		<br/>
		<xsl:apply-templates select="mmlPi:PatientModule"/>
		<xsl:apply-templates select="mmlRe:occupation"/>
		<hr size="2"/>
		<!--	紹介状内容の処理			-->
		<div><font size="+1" color="#0000ff"><b>紹介内容</b></font></div>
		<br/>
		<!--	紹介状タイトルの処理		-->
		<xsl:apply-templates select="mmlRe:title"/>
		<xsl:apply-templates select="mmlRe:greeting"/>
		<table border="1" cellspacing="0" cellpadding="3" width="100%">
			<!--	主訴の処理			-->
			<tr><xsl:apply-templates select="mmlRe:chiefComplaints"/></tr>
			<!--	病名の処理			-->
			<tr><xsl:apply-templates select="mmlRe:clinicalDiagnosis"/></tr>
			<!--	既往歴情報の処理	-->
			<tr><xsl:apply-templates select="mmlRe:pastHistory"/></tr>
			<!--	家族歴情報の処理	-->
			<tr><xsl:apply-templates select="mmlRe:familyHistory"/></tr>
			<!--	現病歴の処理		-->
			<tr><xsl:apply-templates select="mmlRe:presentIllness"/></tr>
			<!--	検査結果の処理		-->
			<tr><xsl:apply-templates select="mmlRe:testResults"/></tr>
			<!--	治療経過の処理		-->
			<tr><xsl:apply-templates select="mmlSm:clinicalCourse"/></tr>
			<!--	現処方の処理		-->
			<tr><xsl:apply-templates select="mmlRe:medication"/></tr>
			<!--	紹介目的の処理		-->
			<tr><xsl:apply-templates select="mmlRe:referPurpose"/></tr>
			<!--	備考の処理			-->
			<tr><xsl:apply-templates select="mmlRe:remarks"/></tr>
		</table>
	</xsl:template>

	<!--	紹介先のテンプレート		-->
	<xsl:template match="mmlRe:referToFacility">
		<div><b>医療機関</b></div>
		<xsl:apply-templates select="mmlFc:Facility"/><br/>
		<div><b>診療科</b></div>
		<xsl:apply-templates select="mmlDp:Department"/><br/>
	</xsl:template>

	<!--	紹介先医師のテンプレート			-->
	<xsl:template match="mmlRe:referToPerson">
		<div><b>医師</b></div>
		<xsl:apply-templates select="mmlPsi:PersonalizedInfo"/><br/>
	</xsl:template>

	<!--	紹介先(Unknown)のテンプレート		-->
	<xsl:template match="mmlRe:referToUnknownName">
		<!-- XHTML使用可	-->
		<xsl:apply-templates/><br/>
	</xsl:template>

	<!--	紹介元情報のテンプレート	-->
	<xsl:template match="mmlRe:referFrom">
		<xsl:apply-templates select="mmlPsi:PersonalizedInfo"/><br/>
	</xsl:template>

	<!--	職業情報のテンプレート		-->
	<xsl:template match="mmlRe:occupation">
		<xsl:if test="*">
			<br/>
			<div><b>患者職業</b></div>
			<!-- XHTML使用可	-->
			<xsl:apply-templates/><br/>
		</xsl:if>
	</xsl:template>

	<!--	タイトルのテンプレート		-->
	<xsl:template match="mmlRe:title">
		<xsl:if test="*">
			<div><b>タイトル</b></div>
			<!-- XHTML使用可	-->
			<xsl:apply-templates/><br/>
		</xsl:if>
	</xsl:template>

	<!--	挨拶文のテンプレート		-->
	<xsl:template match="mmlRe:greeting">
		<xsl:if test="*">
			<div><b>挨拶文</b></div>
			<!-- XHTML使用可	-->
			<xsl:apply-templates/><br/>
		</xsl:if>
	</xsl:template>

	<!--	主訴のテンプレート			-->
	<xsl:template match="mmlRe:chiefComplaints">
		<th bgcolor="#aaaaaa" width="100">主訴</th>
		<td>
			<!-- XHTML使用可	-->
			<xsl:apply-templates/>
		</td>
	</xsl:template>

	<!--	病名のテンプレート			-->
	<xsl:template match="mmlRe:clinicalDiagnosis">
		<th bgcolor="#aaaaaa">病名</th>
		<td>
			<!-- XHTML使用可	-->
			<xsl:apply-templates/>
		</td>
	</xsl:template>

	<!--	既往歴のテンプレート		-->
	<xsl:template match="mmlRe:pastHistory">
		<th bgcolor="#aaaaaa">既往歴</th>
		<td>
			<!-- XHTML使用可	-->
			<xsl:apply-templates/>
		</td>
	</xsl:template>

	<!--	家族歴のテンプレート		-->
	<xsl:template match="mmlRe:familyHistory">
		<th bgcolor="#aaaaaa">家族歴</th>
		<td>
			<!-- XHTML使用可	-->
			<xsl:apply-templates/>
		</td>
	</xsl:template>

	<!--	現病歴のテンプレート		-->
	<xsl:template match="mmlRe:presentIllness">
		<th bgcolor="#aaaaaa">現病歴</th>
		<td>
			<!-- XHTML使用可	-->
			<xsl:apply-templates/>
		</td>
	</xsl:template>

	<!--	検査結果のテンプレート		-->
	<xsl:template match="mmlRe:testResults">
		<th bgcolor="#aaaaaa">検査結果</th>
		<td>
			<!-- XHTML使用可	-->
			<xsl:apply-templates/>
		</td>
	</xsl:template>

	<!--	治療経過のテンプレート		-->
	<xsl:template match="mmlSm:clinicalCourse">
		<th bgcolor="#aaaaaa">治療経過</th>
		<td>
			<xsl:for-each select="mmlSm:clinicalRecord">
				<!-- XHTML使用可	-->
				<xsl:apply-templates/>
			</xsl:for-each>
		</td>
	</xsl:template>

	<!--	現処方のテンプレート		-->
	<xsl:template match="mmlRe:medication">
		<th bgcolor="#aaaaaa">現処方</th>
		<td>
			<!-- XHTML使用可	-->
			<xsl:apply-templates/>
		</td>
	</xsl:template>

	<!--	紹介目的のテンプレート		-->
	<xsl:template match="mmlRe:referPurpose">
		<th bgcolor="#aaaaaa">紹介目的</th>
		<td>
			<!-- XHTML使用可	-->
			<xsl:apply-templates/>
		</td>
	</xsl:template>

	<!--	備考のテンプレート			-->
	<xsl:template match="mmlRe:remarks">
		<th bgcolor="#aaaaaa">備考</th>
		<td>
			<!-- XHTML使用可	-->
			<xsl:apply-templates/>
		</td>
	</xsl:template>

<!--##########################################################################-->

<xsl:template match="content">
	<xsl:apply-templates/>
</xsl:template>

<!--##########################################################################-->

<xsl:template match="mmlLb:TestModule">
	<br/>
	<xsl:apply-templates select="mmlLb:information"/>
	<xsl:for-each select="mmlLb:laboTest">
		<!--<xsl:value-of select="name()"/><br/>-->
		<xsl:apply-templates select="."/>
	</xsl:for-each>
</xsl:template>

<!--##########################################################################-->

<xsl:template match="mmlLb:information">
	<TABLE BORDER="0" CELLPADDING="2">
	<TR ALIGN="LEFT">
		<TD><B>依頼番号: </B><xsl:value-of select="@mmlLb:registId"/></TD>
		<TD><B>検体採取日時: </B><xsl:value-of select="@mmlLb:sampleTime"/></TD>
	</TR>
	<TR ALIGN="LEFT">
		<TD><B>受付日時: </B><xsl:value-of select="@mmlLb:registTime"/></TD>
		<TD><B>報告日時: </B><xsl:value-of select="@mmlLb:reportTime"/></TD>
	</TR>

	<TR ALIGN="LEFT">
		<TD><B>報告状態: </B><xsl:value-of select="mmlLb:reportStatus"/></TD>
		<TD><B>検査セット: </B><xsl:value-of select="mmlLb:set"/></TD>
	</TR>
	<TR ALIGN="LEFT">
		<TD><B>検査依頼施設: </B><xsl:value-of select="mmlLb:facility"/></TD>
		<TD><B>検査依頼診療科: </B><xsl:value-of select="mmlLb:department"/></TD>
	</TR>
	<TR ALIGN="LEFT">
		<TD><B>検査依頼病棟: </B><xsl:value-of select="mmlLb:ward"/></TD>
		<TD><B>検査依頼者: </B><xsl:value-of select="mmlLb:client"/></TD>
	</TR>

	<TR ALIGN="LEFT">
		<TD><B>検査実施施設: </B><xsl:value-of select="mmlLb:laboratoryCenter"/></TD>
		<TD><B>検査実施者: </B><xsl:value-of select="mmlLb:technician"/></TD>
	</TR>

	</TABLE>
</xsl:template>

<!--##########################################################################-->

<xsl:template match="mmlLb:laboTest">
	<xsl:apply-templates select="mmlLb:specimen"/>

	項目数: <xsl:value-of select="count(mmlLb:item)"/><br/>

	<TABLE BORDER="1" CELLPADDING="1">
	<THEAD>
		<!--<TH BGCOLOR="#006699"><FONT COLOR="#FFFFFF">番号</FONT></TH>-->

		<TH BGCOLOR="#006699"><FONT COLOR="#FFFFFF">項目</FONT></TH>
		<TH BGCOLOR="#006699"><FONT COLOR="#FFFFFF">測定値</FONT></TH>
		<TH BGCOLOR="#006699"><FONT COLOR="#FFFFFF">単位</FONT></TH>

		<TH BGCOLOR="#006699"><FONT COLOR="#FFFFFF">下限値</FONT></TH>
		<TH BGCOLOR="#006699"><FONT COLOR="#FFFFFF">上限値</FONT></TH>
		<TH BGCOLOR="#006699"><FONT COLOR="#FFFFFF">基準値</FONT></TH>
		<TH BGCOLOR="#006699"><FONT COLOR="#FFFFFF">異常</FONT></TH>

		<!--<TH BGCOLOR="#006699"><FONT COLOR="#FFFFFF">外部参照情報</FONT></TH>-->
		<TH BGCOLOR="#006699"><FONT COLOR="#FFFFFF">コメント</FONT></TH>
		<!--<TH BGCOLOR="#006699"><FONT COLOR="#FFFFFF">項目フリーコメント</FONT></TH>-->
	</THEAD>

	<xsl:for-each select="mmlLb:item">		
		<TR ALIGN="CENTER">
			<!--<TD BGCOLOR="#CCCFFF"><xsl:number value="position()" format="1"/></TD>-->

			<TD BGCOLOR="#CCCFFF"><xsl:value-of select="mmlLb:itemName"/></TD>
			<TD BGCOLOR="#CCCFFF"><xsl:value-of select="mmlLb:value"/></TD>
			<TD BGCOLOR="#CCCFFF"><xsl:value-of select="mmlLb:unit"/></TD>

			<TD BGCOLOR="#CCCFFF"><xsl:value-of select="mmlLb:numValue/@mmlLb:low"/></TD>
			<TD BGCOLOR="#CCCFFF"><xsl:value-of select="mmlLb:numValue/@mmlLb:up"/></TD>

			<TD BGCOLOR="#CCCFFF"><xsl:value-of select="mmlLb:numValue/@mmlLb:normal"/></TD>

			<!--<TD BGCOLOR="#CCCFFF"><xsl:value-of select="mmlLb:numValue/@mmlLb:out"/></TD>-->
			<TD BGCOLOR="#CCCFFF">
				<xsl:choose>
					<xsl:when test="mmlLb:numValue/@mmlLb:out[.='L']">
					<FONT COLOR="#0000CC">低</FONT>
					</xsl:when>
					<xsl:when test="mmlLb:numValue/@mmlLb:out[.='H']">
					<FONT COLOR="#CC0000">高</FONT>
					</xsl:when>
					<xsl:when test="mmlLb:numValue/@mmlLb:out[.='N']">
					<FONT COLOR="#00CC00">基準内</FONT>
					</xsl:when>
					<xsl:otherwise>
					</xsl:otherwise>
				</xsl:choose>
			</TD>

			<!--<TD BGCOLOR="#CCCFFF"><xsl:value-of select="mmlLb:referenceInfo"/></TD>-->
			<TD BGCOLOR="#CCCFFF"><xsl:value-of select="mmlLb:itemMemo"/></TD>
			<!--<TD BGCOLOR="#CCCFFF"><xsl:value-of select="mmlLb:itemMemoF"/></TD>-->
		</TR>
	</xsl:for-each>

	</TABLE>
</xsl:template>

<!--##########################################################################-->

<xsl:template match="mmlLb:specimen">
	<H3>検体：<xsl:value-of select="."/></H3>
</xsl:template>

<!--##########################################################################-->


</xsl:stylesheet>

